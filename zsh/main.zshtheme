# Reset EVERYTHING!
unset TMOUT
unset RPROMPT
unset RPS1

function current_music () {
  local songData="$(osascript ~/dotfiles/zsh/itunes.scpt)"
  if [[ $songData != "" ]]; then
    echo "%{$fg_bold[cyan]%}[♪ ${songData}]"
  else
    echo ""
  fi
}

function git_get_branch () {
  ref=$(command git symbolic-ref HEAD 2> /dev/null) || \
  ref=$(command git rev-parse --short HEAD 2> /dev/null) || return 0
  echo "${ref#refs/heads/}"
}

function git_get_commit () {
  echo "$(command git rev-parse --short HEAD)"
}

function info_git () {
  if git rev-parse --git-dir > /dev/null 2>&1; then
    local branch=$(git_get_branch)
    local commit=$(git_get_commit)
    echo "%{$fg_bold[magenta]%}[${branch}: ${commit}]"
  fi
}

# TODO - Add the charging icon back
function battery_info () {
  local awk2="$(pmset -g batt | tail -n 1 | awk '{print $3}')"
  local percent="${$(echo $awk2 | grep '%')%?}"
  echo "%{$fg_bold[white]%}[⇀ ${percent}%]"
}

function is_ssh () {
  if [[ -n $SSH_CONNECTION ]]; then
    echo "[SSH]"
  fi
}

function node_version () {
  echo "%{$fg_bold[green]%}[⬢ $(node -v)]"
}

function machine_name () {
  echo "%{$fg_bold[yellow]%}[$(cat $HOME/dotfiles/.nickname)]"
}

function left_prompt () {
  echo "%{$fg_bold[green]%}%T %{$fg_bold[yellow]%}%n %{$fg_bold[red]%}@ %{$fg_bold[cyan]%}%~"
}

function second_prompt () {
  echo "%{$fg_bold[blue]%}→ %{$fg[red]%}"
}

function put_spaces () {
  local zero='%([BSUbfksu]|([FBK]|){*})'
  local upperleft="$(left_prompt)"
  upperleft=${#${(S%%)upperleft//$~zero/}}
  local upperright="$(info_git)$(current_music)$(battery_info)$(machine_name)$(node_version)"
  upperright=${#${(S%%)upperright//$~zero/}}
  local termwidth
  (( termwidth = $(tput cols) - ${upperleft} - ${upperright} ))
  local spacing=""
  for i in {1..$termwidth}; do
    spacing="${spacing} "
  done
  echo $spacing
}

function build_prompt () {
PROMPT='$(left_prompt)$(put_spaces)$(info_git)$(current_music)$(battery_info)$(machine_name)$(node_version)\
 $(second_prompt)'
}

add-zsh-hook precmd build_prompt
